plugins {
    id 'com.android.library' version '8.3.2'
    id 'com.google.protobuf' version '0.9.4'
    id 'de.mannodermaus.android-junit5' version '1.10.0.0'
    id 'maven-publish'
}

group = 'org.osservatorionessuno'
version = '0.1.0-SNAPSHOT'

android {
    namespace 'org.osservatorionessuno.libmvt'
    compileSdk 34

    defaultConfig {
        minSdk 26
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all { useJUnitPlatform() }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.17.+'
    api 'org.ahocorasick:ahocorasick:0.6.3'
    implementation 'com.google.protobuf:protobuf-java:3.25.3'
    implementation 'com.github.StephenOTT:STIX-Java:0.10.0'
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.+'
    implementation 'org.apache.commons:commons-compress:1.26.1'
    compileOnly 'org.immutables:value-annotations:2.10.0'
    compileOnly 'org.immutables:annotate:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.+'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.3'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            from components.release
        }
    }
}
